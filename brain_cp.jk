; BRAIN-CP with WCPMG acquisition
; see Harris et al., JMR, 224 (2012) 38-47
; uses pre-made WURST pulse shapes 
; easiest to phase via a magnitude calculation "mc"

;parameters:
;ns : 16 * n
;d1 : recycle delay
;d2 : echo delay ((d6-p5)/2)
;d3 : time to allow pulse ringdown
;d6 : echo length
;p4 : contact time
;pl1 : not used
;pl2 : not used
;pl9 : power for decoupling
;p3 : 90 degree pulse length
;pl3 : power for 1H 90 degree
;sp0 : proton contact power (typically square pulse, 50 kHz))
;sp4 : X BRAIN contact power (adiabatic inversion pulse)
;p5 : WCPMG pulse length 
;sp5 : WCPMG power level
;spnam5: WURST exc/ref pulses
;spnam4: BRAIN Contact Pulse
;l22 : # of echos to be acquired
;cnst1 : set TD > cnst1
;$OWNER=J. Kimball 

#include <Avance.incl>

#include <De.incl>

"d2=((d6-p5)/2)"
define delay del3
"del3=d3-1u"
"d11=30m"
"cnst1=((d6+d3*2+p5)*l22+d6)/dw"
define delay rest
"rest=aq-(cnst1*dw)"

1 ze

2 d11
  d1
  (p3 pl3 ph1):f2														; 1H 90 pulse
  (p4:sp4 ph2):f1 (p4:sp0 ph10):f2					; contact pulses
  10u reset1:f1															
  1u pl9:f2																	; set decoupling power
  1u cw:f2						
  ACQ_START(ph30,ph31)
  (p5:sp5 ph20):f1													; WURST excitation
  d2																				; echo delay
  del3												
  0.1u START_NEXT_SCAN

3 (p5:sp5 ph21):f1													; WURST refocusing
  del3
  1u 
  d6 REC_UNBLK															; echo length
  1u REC_BLK
  del3
lo to 3 times l22
  1u do:f2
  1u REC_BLK
  rest
  rcyc=2
  d11 mc #0 to 2 F0(zd)

exit

ph0=0
ph1=0 0 0 0 0 0 0 0 2 2 2 2 2 2 2 2

ph2=0
ph10=1

ph20=0 0 1 1 2 2 3 3
ph21=1 3 2 0 3 1 0 2 
ph30=0 
ph31=0 0 1 1 2 2 3 3 2 2 3 3 0 0 1 1


