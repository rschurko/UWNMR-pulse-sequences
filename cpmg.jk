; CPMG Pulse Sequence
; Jun 27, 2022, updated by JK

;parameters:
;ns : 8 * n
;d1 : recycle delay
;d2 : echo delay ((d6-p5)/2)
;d3 : time to allow pulse ringdown, 10 - 100 us
;d6 : echo length
;pl9 : decoupling power
;pl6 : excitation/refocusing RF power level
;pl1 : not used
;pl2 : not used
;p5 : excitation pulse length
;p6 : refocusing pulse length
;l22 : # of echos to be acquired
;cnst1 : set TD > cnst1
;$OWNER=J Kimball
 
#include <Avance.incl>
#include <De.incl>

"d11=30m"
"d2=((d6-p5)/2)"
define delay del3
"del3=d3-1u"
"cnst1=((d6+d3*2+p6)*l22+d6)/dw"
define delay rest  			
"rest=aq-(cnst1*dw)"


1 ze													 ; sets all phases such that they are the first element after dummy scans

2 d11 do:f2
  d1
  1u pl9:f2										 ; set decoupling power
  10u reset1:f1							   ; reset NCO1 to 0 on channel 1
  ACQ_START(ph30,ph31)
  1u cw:f2
  (p5 pl6 ph1):f1							 ; pi/2 excitation
  d2						   						 ; echo delay 	
  del3									       ; ring down delay	
  0.1u START_NEXT_SCAN 									
  
  3 (p6 pl6 ph2):f1				     ; pi refocusing pulse
  del3									
  1u 			
  d6 REC_UNBLK								 ; echo length						     
  1u REC_BLK							
  del3									
lo to 3 times l22		
  1u do:f2
  rest
  1u
  rcyc=2
  d11 mc #0 to 2 F0(zd)
  
exit

ph0=0
ph1=0 0 1 1 2 2 3 3 
ph2=1 3 2 0 3 1 0 2
ph30=0
ph31=0 0 1 1 2 2 3 3
